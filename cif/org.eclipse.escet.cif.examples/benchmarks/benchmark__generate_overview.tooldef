//////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, 2023 Contributors to the Eclipse Foundation
//
// See the NOTICE file(s) distributed with this work for additional
// information regarding copyright ownership.
//
// This program and the accompanying materials are made available
// under the terms of the MIT License which is available at
// https://opensource.org/licenses/MIT
//
// SPDX-License-Identifier: MIT
//////////////////////////////////////////////////////////////////////////////

// Generates overview HTML file for benchmark results.

import java.lang.Long.decode;
import java.lang.Long.valueOf;
import org.apache.commons.lang3:org.apache.commons.lang3.StringEscapeUtils.escapeHtml4 as escape;

// Initialize metrics info:
// - metrics_*: from benchmark model name (column) to configuration (row) to metric value (cell).
// - configs: the configurations used for benchmarking.
// - names: the names of the models used for benchmarking.
map(string: map(int: long)) metrics_nodes = {};
map(string: map(int: long)) metrics_ops   = {};
list string configs;
list string names;

// Find benchmarking results.
list string file_names = sorted(find("_generated", "*.metrics.csv"));
if size(file_names) == 0:
    errln("No benchmark results found. Execute one or more benchmarks before generating an overview of the results.");
    exit 1;
end

// Process the benchmark metrics files.
bool first_file = true;
for f in file_names:
    // Get benchmark model name.
    string name = replace(f, ".metrics.csv", "");
    metrics_nodes[name] = {};
    metrics_ops  [name] = {};
    names = names + [name];

    // Get benchmark experiment lines. Remove header line.
    list string lines = readlines(pathjoin("_generated", f));
    lines = delidx(lines, 0);

    // Process the experiment lines.
    for line in lines:
        list string parts = split(line, ",");
        int config_nr = <int>valueOf(trim(parts[0]));
        long nodes_cnt = <long>valueOf(trim(parts[2]));
        long ops_cnt   = <long>valueOf(trim(parts[3]));
        metrics_nodes[name][config_nr] = nodes_cnt;
        metrics_ops  [name][config_nr] = ops_cnt;
        if first_file:
            string config = join(parts[4:], ",");
            config = trim(config);
            if startswith(config, "\""):: config = config[1:];
            if endswith  (config, "\""):: config = config[:-1];
            configs = configs + [config];
        end
    end

    // Next file is no longer first file.
    first_file = false;
end

// Calculate best and worst values, per model.
long max_long = <long>decode("0x7fffffffffffffff");
list long best_values_nodes;
list long best_values_ops;
list long worst_values_nodes;
list long worst_values_ops;
for name in names:
    long best_value_nodes  = max_long;
    long best_value_ops    = max_long;
    long worst_value_nodes = -1;
    long worst_value_ops   = -1;
    for value in values(metrics_nodes[name]):: best_value_nodes  = min(best_value_nodes , value);
    for value in values(metrics_ops  [name]):: best_value_ops    = min(best_value_ops   , value);
    for value in values(metrics_nodes[name]):: worst_value_nodes = max(worst_value_nodes, value);
    for value in values(metrics_ops  [name]):: worst_value_ops   = max(worst_value_ops  , value);
    best_values_nodes  = best_values_nodes  + [best_value_nodes];
    best_values_ops    = best_values_ops    + [best_value_ops  ];
    worst_values_nodes = worst_values_nodes + [worst_value_nodes];
    worst_values_ops   = worst_values_ops   + [worst_value_ops  ];
end

// Construct the HTML file contents.
list string html_lines = [
    "<!DOCTYPE html>",
    "<html lang='en'>",
    "  <head>",
    "    <title>Benchmark results overview</title>",
    "    <meta charset='utf-8'>",
    "    <style>",
    "      h2 {",
    "        margin: 0;",
    "      }",
    "      .section {",
    "        margin-top: 20px;",
    "      }",
    "      table {",
    "        margin-top: 2px;",
    "      }",
    "      table.legend {",
    "        border: solid 2px black;",
    "        border-spacing: 0px;",
    "      }",
    "      table.legend td {",
    "        white-space: nowrap;",
    "      }",
    "      table.legend td.metric {",
    "        min-width: 200px;",
    "      }",
    "      table.legend td.sep {",
    "        background: black;",
    "      }",
    "      table.legend td.text {",
    "        padding-left: 4px;",
    "        padding-right: 4px;",
    "      }",
    "      table.legend td.text.left {",
    "        text-align: left;",
    "        padding-right: 20px;",
    "      }",
    "      table.legend td.text.right {",
    "        text-align: right;",
    "        padding-left: 20px;",
    "      }",
    "      table.main {",
    "        border-spacing: 0;",
    "      }",
    "      table.main tr.last-row td {",
    "        border-bottom: solid 2px black;",
    "      }",
    "      table.main th {",
    "        background: black;",
    "        color: white;",
    "      }",
    "      table.main td, table.main th {",
    "        padding: 2px 5px;",
    "        border: solid 1px black;",
    "        text-align: left;",
    "        margin: 0;",
    "      }",
    "      table.main td:first-of-type, table.main th:first-of-type {",
    "        border-left: solid 2px black;",
    "      }",
    "      table.main td:last-of-type, table.main th:last-of-type {",
    "        border-right: solid 2px black;",
    "      }",
    "      table.main th.name {",
    "        text-align: center;",
    "      }",
    "      table.main td.metric {",
    "        text-align: right;",
    "      }",
    "      table.legend td.metric.best {",
    "        background: #80ff00;",
    "      }",
    "      table.main td.metric.best {",
    "        background: #80ff00;",
    "        font-weight: bold;",
    "      }",
    "      @keyframes worse1-frames {",
    "        0% {",
    "          background-color: #80ff00;",
    "        }",
    "        33.3% {",
    "          background-color: yellow;",
    "        }",
    "        100% {",
    "          background-color: orange;",
    "        }",
    "      }",
    "      @keyframes worse2-frames {",
    "        0% {",
    "          background-color: orange;",
    "        }",
    "        100% {",
    "          background-color: orangered;",
    "        }",
    "      }",
    "      table.main td.metric.worse1-color {",
    "        animation: 100.001s linear calc(-1s * var(--percentage)) paused worse1-frames;",
    "      }",
    "      table.main td.metric.worse2-color {",
    "        animation: 100.001s linear calc(-1s * var(--percentage)) paused worse2-frames;",
    "      }",
    "      table.legend td.metric.worse1-gradient {",
    "        background: linear-gradient(to right, #80ff00 0%, yellow 33.3%, orange 100%);",
    "      }",
    "      table.legend td.metric.worse2-gradient {",
    "        background: linear-gradient(to right, orange 0%, orangered 100%);",
    "      }",
    "      table.main td.dummy {",
    "        border: 0;",
    "        padding-left: 0;",
    "      }",
    "    </style>",
    "  </head>",
    "  <body>",
    "    <h2>CIF data-based synthesis benchmark results overview</h2>",
    "    <div class='section'>Legend (per metric, per model/column):</div>",
    "    <table class='legend'>",
    "      <tr>",
    "        <td class='text'>Best configuration/row</td>",
    "        <td class='sep'</td>",
    "        <td class='text' colspan='2'>A bit worse</td>",
    "        <td class='sep'</td>",
    "        <td class='text' colspan='2'>Even worse</td>",
    "      </tr>",
    "      <tr>",
    "        <td class='metric best'>&nbsp;</td>",
    "        <td class='sep'</td>",
    "        <td class='metric worse1-gradient' colspan='2'>&nbsp;</td>",
    "        <td class='sep'</td>",
    "        <td class='metric worse2-gradient' colspan='2'>&nbsp;</td>",
    "      </tr>",
    "      <tr>",
    "        <td></td> <!-- Best -->",
    "        <td class='sep'</td>",
    "        <td class='text left'>&times;1</td>",
    "        <td class='text right'>&times;2</td>",
    "        <td class='sep'</td>",
    "        <td class='text left'>&times;2</td>",
    "        <td class='text right'>worst</td>",
    "      </tr>",
    "    </table>",
    "    <table class='main section'>",
];

list string lines_nodes;
list string lines_ops;

lines_nodes = lines_nodes + ["      <tr>"];
lines_ops   = lines_ops   + ["      <tr>"];
lines_nodes = lines_nodes + ["        <th>Nr</th>"];
lines_ops   = lines_ops   + ["        <th>Nr</th>"];
lines_nodes = lines_nodes + ["        <th>Configuration</th>"];
lines_ops   = lines_ops   + ["        <th>Configuration</th>"];
for name in names:
    lines_nodes = lines_nodes + [fmt("        <th class='name'>%s</th>", escape(name))];
    lines_ops   = lines_ops   + [fmt("        <th class='name'>%s</th>", escape(name))];
end
lines_nodes = lines_nodes + ["      </tr>"];
lines_ops   = lines_ops   + ["      </tr>"];

tool tuple(string, string) get_classes_and_style(long value, long best_value, long worst_value):
    // Handle best value.
    if value == best_value:: return " best", "";

    // Not the best value. Get the percentage that it is worse than the best value.
    double worse_percentage = ((value / best_value) - 1) * 100;

    // Handle being at most 100% worse, with a gradient.
    if worse_percentage <= 100:
        return " worse1-color", fmt("--percentage:%.3f", worse_percentage);
    end

    // Handle being more than 100% worse, with a different gradient. Scaled as percentage to maximum value.
    double worst_percentage = (value - 2*best_value) / (worst_value - 2*best_value) * 100;
    return " worse2-color", fmt("--percentage:%.3f", worst_percentage);
end

for idx, config in enumerate(configs):
    if empty(trim(config)):: config = "(default)";
    bool last_config = idx == size(configs) - 1;
    string close_extra_classes;
    if last_config:: close_extra_classes = close_extra_classes + " class='last-row'";
    lines_nodes = lines_nodes + [fmt("      <tr%s>", close_extra_classes)];
    lines_ops   = lines_ops   + [fmt("      <tr%s>", close_extra_classes)];
    lines_nodes = lines_nodes + [fmt("        <td class='nr'>%d/%d</td>", idx + 1, size(configs))];
    lines_ops   = lines_ops   + [fmt("        <td class='nr'>%d/%d</td>", idx + 1, size(configs))];
    lines_nodes = lines_nodes + [fmt("        <td class='config'>%s</td>", escape(config))];
    lines_ops   = lines_ops   + [fmt("        <td class='config'>%s</td>", escape(config))];
    for idx2, name in enumerate(names):
        long value_nodes = metrics_nodes[name][idx + 1];
        long value_ops   = metrics_ops  [name][idx + 1];
        string text_nodes = <string>escape(fmt("%,d", value_nodes));
        string text_ops   = <string>escape(fmt("%,d", value_ops));
        tuple(string, string) cs_nodes = get_classes_and_style(value_nodes, best_values_nodes[idx2], worst_values_nodes[idx2]);
        tuple(string, string) cs_ops   = get_classes_and_style(value_ops  , best_values_ops  [idx2], worst_values_ops  [idx2]);
        lines_nodes = lines_nodes + [fmt("        <td class='metric%s' style='%s'>%s</td>", cs_nodes[0], cs_nodes[1], text_nodes)];
        lines_ops   = lines_ops   + [fmt("        <td class='metric%s' style='%s'>%s</td>", cs_ops[0],   cs_ops[1],   text_ops  )];
    end
    lines_nodes = lines_nodes + ["      </tr>"];
    lines_ops   = lines_ops   + ["      </tr>"];
end

html_lines = html_lines + ["      <tr><td class='dummy' colspan='99'>Maximum number of used BDD nodes:</td></tr>"];
html_lines = html_lines + lines_nodes;
html_lines = html_lines + ["      <tr><td class='dummy' colspan='99'>&nbsp;</td></tr>"];
html_lines = html_lines + ["      <tr><td class='dummy' colspan='99'>Number of BDD operations:</td></tr>"];
html_lines = html_lines + lines_ops;

html_lines = html_lines + [
    "    </table>",
    "  </body>",
    "</html>"
];

// Write the HTML file.
string html_filepath = replace(pathjoin("_generated", "_overview.html"), "\\", "/");
writefile(html_filepath, html_lines);

// Done.
outln("Processed %,d benchmark result(s), for %,d configuration(s) each.", size(names), size(configs));
outln("See \"%s\" for the generated overview.", html_filepath);

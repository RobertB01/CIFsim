//////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2010, 2024 Contributors to the Eclipse Foundation
//
// See the NOTICE file(s) distributed with this work for additional
// information regarding copyright ownership.
//
// This program and the accompanying materials are made available
// under the terms of the MIT License which is available at
// https://opensource.org/licenses/MIT
//
// SPDX-License-Identifier: MIT
//////////////////////////////////////////////////////////////////////////////

from "lib:cif" import cifevtdis, cif2cif;

// Configuration.
string test_path = "event_disabler";
string test_pattern = "*.cif";
list string default_options = ["--devmode=1"];
map(string:list string) test_options = {
    "event_disabler/aut_rename.cif":                       ["-e event_disabler"],
    "event_disabler/controllability.cif":                  ["-udisable -e e,e_e,c,c_c,u,u_u,g.a.e,g.a.e_e,g.a.c,g.a.c_c,g.a.u,g.a.u_u"],
    "event_disabler/empty.cif":                            ["-f empty.txt"],
    "event_disabler/event_is_comp.cif":                    ["-e a.b.c,a.b"],
    "event_disabler/event_is_decl.cif":                    ["-i1 -e a.b"],
    "event_disabler/invalid_event_name.cif":               ["-e a.%b.c"],
    "event_disabler/opt_event_names.cif":                  ["-e \" a a b , c, d.e ,\""],
    "event_disabler/opt_event_names_file.cif":             ["-f event_disabler/opt_event_names_file.events"],
    "event_disabler/opt_event_names_multiple_options.cif": ["-e name1,name2,g.name3,all -f event_disabler/opt_event_names_multiple_options.events -s1"],
    "event_disabler/opt_event_names_none.cif":             [""],
    "event_disabler/opt_event_svgin.cif":                  ["-s1"],
    "event_disabler/parent_aut.cif":                       ["-i1 -e g.a.b.c"],
    "event_disabler/parent_decl.cif":                      ["-e a.b,a.b.c"],
    "event_disabler/remove_controller_properties.cif":     ["-e c"],
    "event_disabler/usage_alphabet_incl_input.cif":        ["-ualphabet -i1 -e is,ns,new,g.is,g.ns,g.new,g.a.is,g.a.ns,g.a.new"],
    "event_disabler/usage_alphabet_no_input.cif":          ["-ualphabet -i0 -e is,ns,new,g.is,g.ns,g.new,g.a.is,g.a.ns,g.a.new"],
    "event_disabler/usage_disable_incl_input.cif":         ["-udisable -i1 -e is,ns,new,g.is,g.ns,g.new,g.a.is,g.a.ns,g.a.new"],
    "event_disabler/usage_disable_no_input.cif":           ["-udisable -i0 -e is,ns,new,g.is,g.ns,g.new,g.a.is,g.a.ns,g.a.new"],
};
set string test_skip = {
   "event_disabler/opt_event_names_file_not_exist.cif", //"-f event_disabler/does_not_exist.events") -> test case disabled: contains absolute path
};

// Initialize counts.
int run_count = 0;
int run_successes = 0;
int run_failures = 0;
int run_skipped = 0;

int check_count = 0;
int check_successes = 0;
int check_failures = 0;

// Find tests.
list string tests = find(test_path, test_pattern);
for i in range(tests):: tests[i] = replace(pathjoin(test_path, tests[i]), "\\", "/");
for i in reverse(range(tests)):
    if contains(tests[i], ".disabled.cif"):
        tests = delidx(tests, i);
        continue;
    end
    if contains(test_skip, tests[i]):
        tests = delidx(tests, i);
        run_count = run_count + 1;
        run_skipped = run_skipped + 1;
    end
end

// Test all tests.
for test in tests:
    // ====
    // Run.
    // ====

    // Get test specific options.
    list string options = default_options;
    list string extra_options;
    if contains(test_options, test):: extra_options = test_options[test];
    options = options + extra_options;

    // Print what we are testing.
    outln("Testing \"%s\" (run) using options \"%s\"...", test, join(extra_options, " "));

    // Get paths.
    string test1_out_exp  = chfileext(test, "cif", "run.out");
    string test1_err_exp  = chfileext(test, "cif", "run.err");
    string test1_out_real = chfileext(test, "cif", "run.out.real");
    string test1_err_real = chfileext(test, "cif", "run.err.real");
    string rslt1_exp      = chfileext(test, "cif", "disabled.cif");
    string rslt1_real     = chfileext(test, "cif", "disabled.cif.real");

    // Execute.
    list string options1 = options + ["-o", rslt1_real];
    int exit_code1 = cifevtdis([test] + options1, stdout=test1_out_real, stderr=test1_err_real, ignoreNonZeroExitCode=true);

    // Compare output.
    bool stderr1_diff = diff(test1_err_exp, test1_err_real, missingAsEmpty=true, warnOnDiff=true);
    bool stdout1_diff = diff(test1_out_exp, test1_out_real, missingAsEmpty=true, warnOnDiff=true);
    bool rslt1_diff   = diff(rslt1_exp, rslt1_real, missingAsEmpty=true, warnOnDiff=true);
    if not stderr1_diff:: rmfile(test1_err_real);
    if not stdout1_diff:: rmfile(test1_out_real);
    // Don't remove 'rslt1_real' here, as we need to check it still.

    // Update counts.
    int run_diff_count = 0;
    if stderr1_diff:: run_diff_count = run_diff_count + 1;
    if stdout1_diff:: run_diff_count = run_diff_count + 1;
    if rslt1_diff::   run_diff_count = run_diff_count + 1;

    run_count = run_count + 1;
    if run_diff_count == 0:: run_successes = run_successes + 1;
    if run_diff_count > 0:: run_failures = run_failures + 1;

    // =========================
    // Check produced CIF model.
    // =========================

    // Get paths.
    string test2          = rslt1_real;
    string file2_out_real = chfileext(test, "cif", "check.out.real.cif");

    // Do check, if applicable.
    if exit_code1 == 0 or isfile(rslt1_exp) or isfile(rslt1_real):
        // Print what we are testing.
        outln("Testing \"%s\" (check)...", test);

        // Execute.
        list string options2 = ["--devmode=1", "--output-mode=error", "-o", file2_out_real];
        int exit_code2 = cif2cif([test2] + options2, ignoreNonZeroExitCode=true);

        // Check output.
        bool exit_code2_ok = (exit_code2 == 0);
        bool file2_written = isfile(file2_out_real) and filesize(file2_out_real) > 0;
        bool check_ok = exit_code2_ok and file2_written;

        // Cleanup.
        if check_ok and exists(file2_out_real):: rmfile(file2_out_real);

        // Update counts.
        check_count = check_count + 1;
        if     check_ok:: check_successes = check_successes + 1;
        if not check_ok:: check_failures  = check_failures  + 1;
    end

    // ========
    // Cleanup.
    // ========

    if not rslt1_diff:: if exists(rslt1_real):: rmfile(rslt1_real);
end

// Get result messages.
string run_rslt;
string check_rslt;
if run_failures   == 0: run_rslt   = "SUCCESS"; else run_rslt   = "FAILURE"; end
if check_failures == 0: check_rslt = "SUCCESS"; else check_rslt = "FAILURE"; end

string run_msg   = fmt("Test %s (%s, run):   %d tests, %d successes, %d failures, %d skipped.",
                       run_rslt, test_path, run_count, run_successes, run_failures, run_skipped);
string check_msg = fmt("Test %s (%s, check): %d tests, %d successes, %d failures.",
                       check_rslt, test_path, check_count, check_successes, check_failures);

// Output result messages.
if run_failures == 0:
    outln(run_msg);
else
    errln(run_msg);
end

if check_failures == 0:
    outln(check_msg);
else
    errln(check_msg);
end

// Return number of failures as exit code. No failures means zero exit code,
// any failures means non-zero exit code.
exit run_failures + check_failures;

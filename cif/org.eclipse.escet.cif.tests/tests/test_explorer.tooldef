//////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2010, 2024 Contributors to the Eclipse Foundation
//
// See the NOTICE file(s) distributed with this work for additional
// information regarding copyright ownership.
//
// This program and the accompanying materials are made available
// under the terms of the MIT License which is available at
// https://opensource.org/licenses/MIT
//
// SPDX-License-Identifier: MIT
//////////////////////////////////////////////////////////////////////////////

from "lib:cif" import cifexplorer, cif2cif;

// Configuration.
string test_path = "explorer";
string test_pattern = "*.cif";
list string default_options = ["--devmode=1", "-e1", "-s0", "-z0"];
map(string:list string) test_options = {
    "explorer/add_state_annos_option_off.cif":  ["--add-state-annos=off"],
    "explorer/add_state_annos_option_on.cif":   ["--add-state-annos=on"],
    "explorer/inv_limit.cif":                   ["-s1"],
    "explorer/no_initial_state.cif":            ["-s1"],
    "explorer/non_deterministic_no_mini.cif":   ["-z0"],
    "explorer/non_deterministic_with_mini.cif": ["-z1"],
    "explorer/no_initial_state.cif":            ["-s1"],
    "explorer/name_option1.cif":                ["-n newName"],
    "explorer/name_option2.cif":                ["-n a"],
};
set string test_skip = {};

// Initialize counts.
int run_count = 0;
int run_successes = 0;
int run_failures = 0;
int run_skipped = 0;

int check_count = 0;
int check_successes = 0;
int check_failures = 0;

// Find tests.
list string tests = find(test_path, test_pattern);
for i in range(tests):: tests[i] = replace(pathjoin(test_path, tests[i]), "\\", "/");
for i in reverse(range(tests)):
    if contains(tests[i], ".aut.cif"):
        tests = delidx(tests, i);
        continue;
    end
    if contains(test_skip, tests[i]):
        tests = delidx(tests, i);
        run_count = run_count + 1;
        run_skipped = run_skipped + 1;
    end
end

// Test all tests.
for test in tests:
    // ====
    // Run.
    // ====

    // Get test specific options.
    list string options = default_options;
    list string extra_options;
    if contains(test_options, test):: extra_options = test_options[test];
    options = options + extra_options;

    // Print what we are testing.
    outln("Testing \"%s\" (run) using options \"%s\"...", test, join(extra_options, " "));

    // Get paths.
    string test1_out_exp  = chfileext(test, "cif", "run.out");
    string test1_err_exp  = chfileext(test, "cif", "run.err");
    string test1_out_real = chfileext(test, "cif", "run.out.real");
    string test1_err_real = chfileext(test, "cif", "run.err.real");

    string report1_exp    = chfileext(test, "cif", "report.txt");
    string report1_real   = chfileext(test, "cif", "report.txt.real");
    string aut1_exp       = chfileext(test, "cif", "aut.cif");
    string aut1_real      = chfileext(test, "cif", "aut.cif.real");

    // Execute.
    list string options1 = options + ["-r", report1_real, "-o", aut1_real];
    int exit_code1 = cifexplorer([test] + options1, stdout=test1_out_real, stderr=test1_err_real, ignoreNonZeroExitCode=true);

    // Compare output.
    bool stderr1_diff = diff(test1_err_exp, test1_err_real, missingAsEmpty=true, warnOnDiff=true);
    bool stdout1_diff = diff(test1_out_exp, test1_out_real, missingAsEmpty=true, warnOnDiff=true);
    bool report1_diff = diff(report1_exp, report1_real, missingAsEmpty=true, warnOnDiff=true);
    bool aut1_diff    = diff(aut1_exp, aut1_real, missingAsEmpty=true, warnOnDiff=true);
    if not stderr1_diff:: rmfile(test1_err_real);
    if not stdout1_diff:: rmfile(test1_out_real);
    if not report1_diff:: if exists(report1_real):: rmfile(report1_real);
    // Don't remove 'aut1_real' here, as we need to check it still.

    // Update counts.
    int run_diff_count = 0;
    if stderr1_diff:: run_diff_count = run_diff_count + 1;
    if stdout1_diff:: run_diff_count = run_diff_count + 1;
    if report1_diff:: run_diff_count = run_diff_count + 1;
    if aut1_diff::    run_diff_count = run_diff_count + 1;

    run_count = run_count + 1;
    if run_diff_count == 0:: run_successes = run_successes + 1;
    if run_diff_count > 0:: run_failures = run_failures + 1;

    // =========================
    // Check produced CIF model.
    // =========================

    // Get paths.
    string test2          = aut1_real;
    string file2_out_real = chfileext(test, "cif", "check.out.real.cif");

    // Do check, if applicable.
    if exit_code1 == 0 or isfile(aut1_exp) or isfile(aut1_real):
        // Print what we are testing.
        outln("Testing \"%s\" (check)...", test);

        // Execute.
        list string options2 = ["--devmode=1", "--output-mode=error", "-o", file2_out_real];
        int exit_code2 = cif2cif([test2] + options2, ignoreNonZeroExitCode=true);

        // Check output.
        bool exit_code2_ok = (exit_code2 == 0);
        bool file2_written = isfile(file2_out_real) and filesize(file2_out_real) > 0;
        bool check_ok = exit_code2_ok and file2_written;

        // Cleanup.
        if check_ok and exists(file2_out_real):: rmfile(file2_out_real);

        // Update counts.
        check_count = check_count + 1;
        if     check_ok:: check_successes = check_successes + 1;
        if not check_ok:: check_failures  = check_failures  + 1;
    end

    // ========
    // Cleanup.
    // ========

    if not aut1_diff:: if exists(aut1_real):: rmfile(aut1_real);
end

// Get result messages.
string run_rslt;
string check_rslt;
if run_failures   == 0: run_rslt   = "SUCCESS"; else run_rslt   = "FAILURE"; end
if check_failures == 0: check_rslt = "SUCCESS"; else check_rslt = "FAILURE"; end

string run_msg   = fmt("Test %s (%s, run):   %d tests, %d successes, %d failures, %d skipped.",
                       run_rslt, test_path, run_count, run_successes, run_failures, run_skipped);
string check_msg = fmt("Test %s (%s, check): %d tests, %d successes, %d failures.",
                       check_rslt, test_path, check_count, check_successes, check_failures);

// Output result messages.
if run_failures == 0:
    outln(run_msg);
else
    errln(run_msg);
end

if check_failures == 0:
    outln(check_msg);
else
    errln(check_msg);
end

// Return number of failures as exit code. No failures means zero exit code,
// any failures means non-zero exit code.
exit run_failures + check_failures;

//////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2010, 2024 Contributors to the Eclipse Foundation
//
// See the NOTICE file(s) distributed with this work for additional
// information regarding copyright ownership.
//
// This program and the accompanying materials are made available
// under the terms of the MIT License which is available at
// https://opensource.org/licenses/MIT
//
// SPDX-License-Identifier: MIT
//////////////////////////////////////////////////////////////////////////////

from "lib:cif" import cif2cif;

// Configuration.
string test_path = "cif2cif";
string test_pattern = "*.cif";
list string default_options = ["--devmode=1"];
map(string:list string) test_options = {
   "cif2cif/add_default_init_values.cif":           ["-t add-default-init-values"],
   "cif2cif/anonymize_names.cif":                   ["-t anonymize-names"],
   "cif2cif/convert-uncntrl-events-to-cntrl.cif":   ["-t convert-uncntrl-events-to-cntrl"],
   "cif2cif/convert-cntrl-events-to-uncntrl.cif":   ["-t convert-cntrl-events-to-uncntrl"],
   "cif2cif/elim_alg_vars1.cif":                    ["-t elim-alg-vars"],
   "cif2cif/elim_alg_vars2.cif":                    ["-t elim-comp-def-inst,elim-self,elim-alg-vars"],
   "cif2cif/elim_aut_casts.cif":                    ["-t elim-comp-def-inst,elim-aut-casts"],
   "cif2cif/elim_consts.cif":                       ["-t elim-consts"],
   "cif2cif/elim_equations.cif":                    ["-t elim-equations"],
   "cif2cif/elim_groups.cif":                       ["-t elim-groups"],
   "cif2cif/elim_if_updates.cif":                   ["-t elim-if-updates"],
   "cif2cif/elim_locs_in_exprs.cif":                ["-t elim-locs-in-exprs"],
   "cif2cif/elim_monitors.cif":                     ["-t elim-monitors"],
   "cif2cif/elim_self.cif":                         ["-t elim-comp-def-inst,elim-self"],
   "cif2cif/elim_state_evt_excl_invs.cif":          ["-t elim-state-evt-excl-invs"],
   "cif2cif/elim_tau_event.cif":                    ["-t elim-tau-event"],
   "cif2cif/elim_tuple_field_projs.cif":            ["-t elim-tuple-field-projs"],
   "cif2cif/elim_type_decls.cif":                   ["-t elim-type-decls"],
   "cif2cif/enums_to_consts1.cif":                  ["-t elim-comp-def-inst,enums-to-consts"],
   "cif2cif/enums_to_consts2.cif":                  ["-t enums-to-consts"],
   "cif2cif/enums_to_ints1.cif":                    ["-t elim-comp-def-inst,enums-to-ints"],
   "cif2cif/enums_to_ints2.cif":                    ["-t enums-to-ints"],
   "cif2cif/lift_events.cif":                       ["-t lift-events"],
   "cif2cif/linearize_merge1.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge2.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge3.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge4.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge5.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge6.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge7.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_merge8.cif":                  ["-t linearize-merge"],
   "cif2cif/linearize_product1.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product2.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product3.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product4.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product5.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product6.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product7.cif":                ["-t linearize-product"],
   "cif2cif/linearize_product8.cif":                ["-t linearize-product"],
   "cif2cif/merge_enums1.cif":                      ["-t merge-enums"],
   "cif2cif/merge_enums2.cif":                      ["-t merge-enums"],
   "cif2cif/print_file_into_decls.cif":             ["-t elim-comp-def-inst,print-file-into-decls"],
   "cif2cif/remove_annos.cif":                      ["-t remove-annos"],
   "cif2cif/remove_cif_svg_decls.cif":              ["-t remove-cif-svg-decls"],
   "cif2cif/remove_io_decls.cif":                   ["-t remove-io-decls"],
   "cif2cif/remove_print_decls.cif":                ["-t remove-print-decls"],
   "cif2cif/remove_reqs.cif":                       ["-t remove-reqs"],
   "cif2cif/remove_reqs_invalid.cif":               ["-t remove-reqs"],
   "cif2cif/remove_unused_alg_vars.cif":            ["-t remove-unused-alg-vars"],
   "cif2cif/remove_unused_events.cif":              ["-t remove-unused-events"],
   "cif2cif/simplify_others.cif":                   ["-t simplify-others"],
   "cif2cif/simplify_values.cif":                   ["-t simplify-values"],
   "cif2cif/simplify_values_optimized.cif":         ["-t simplify-values-optimized"],
   "cif2cif/simplify_values_no_refs.cif":           ["-t simplify-values-no-refs"],
   "cif2cif/simplify_values_no_refs_optimized.cif": ["-t simplify-values-no-refs-optimized"],
   "cif2cif/svg_file_into_decls.cif":               ["-t elim-comp-def-inst,svg-file-into-decls"],
   "cif2cif/switches_to_ifs.cif":                   ["-t switches-to-ifs"],
};
set string test_skip = {};

// Initialize counts.
int trans_count = 0;
int trans_successes = 0;
int trans_failures = 0;
int trans_skipped = 0;

int check_count = 0;
int check_successes = 0;
int check_failures = 0;

// Find tests.
list string tests = find(test_path, test_pattern);
for i in range(tests):: tests[i] = replace(pathjoin(test_path, tests[i]), "\\", "/");
for i in reverse(range(tests)):
    if contains(tests[i], ".out.cif") or endswith(tests[i], ".out.real.cif"):
        tests = delidx(tests, i);
        continue;
    end
    if contains(test_skip, tests[i]):
        tests = delidx(tests, i);
        trans_count = trans_count + 1;
        trans_skipped = trans_skipped + 1;
    end
end

// Check completeness of test specific options.
if size(test_options) != size(tests):
    errln("CIF to CIF tests: %d tests, %d test specific options.",
          size(tests), size(test_options));
    exit 1;
end

// Test all tests.
for test in tests:
    // ==========
    // Transform.
    // ==========

    // Get test specific options.
    list string options = default_options;
    list string extra_options;
    if contains(test_options, test):: extra_options = test_options[test];
    options = options + extra_options;

    // Print what we are testing.
    outln("Testing \"%s\" (transform) using options \"%s\"...", test, join(extra_options, " "));

    // Get paths.
    string test1_out_exp  = chfileext(test, "cif", "cif2cif.out");
    string test1_err_exp  = chfileext(test, "cif", "cif2cif.err");
    string test1_out_real = chfileext(test, "cif", "cif2cif.out.real");
    string test1_err_real = chfileext(test, "cif", "cif2cif.err.real");
    string file1_out_exp  = chfileext(test, "cif", "cif2cif.out.cif");
    string file1_out_real = chfileext(test, "cif", "cif2cif.out.real.cif");

    // Execute.
    list string options1 = options + ["-o", file1_out_real];
    int exit_code1 = cif2cif([test] + options1, stdout=test1_out_real, stderr=test1_err_real, ignoreNonZeroExitCode=true);

    // Compare stdout/stderr.
    bool stderr1_diff = diff(test1_err_exp, test1_err_real, missingAsEmpty=true, warnOnDiff=true);
    bool stdout1_diff = diff(test1_out_exp, test1_out_real, missingAsEmpty=true, warnOnDiff=true);
    bool file1_diff   = diff(file1_out_exp, file1_out_real, missingAsEmpty=true, warnOnDiff=true);
    if not stderr1_diff:: rmfile(test1_err_real);
    if not stdout1_diff:: rmfile(test1_out_real);
    // Don't remove 'file1_out_real' here, as we need to check it still.

    // Update counts.
    int trans_diff_count = 0;
    if stderr1_diff:: trans_diff_count = trans_diff_count + 1;
    if stdout1_diff:: trans_diff_count = trans_diff_count + 1;
    if file1_diff::   trans_diff_count = trans_diff_count + 1;

    trans_count = trans_count + 1;
    if trans_diff_count == 0:: trans_successes = trans_successes + 1;
    if trans_diff_count > 0:: trans_failures = trans_failures + 1;

    // =========================
    // Check produced CIF model.
    // =========================

    // Get paths.
    string test2          = file1_out_real;
    string test2_out_real = chfileext(test, "cif", "check.out.real");
    string test2_err_real = chfileext(test, "cif", "check.err.real");
    string file2_out_real = chfileext(test, "cif", "check.out.real.cif");

    // Do check, if applicable.
    if exit_code1 == 0 or isfile(file1_out_exp) or isfile(file1_out_real):
        // Print what we are testing.
        outln("Testing \"%s\" (check)...", test);

        // Execute.
        list string options2 = ["--devmode=1", "-o", file2_out_real];
        int exit_code2 = cif2cif([test2] + options2, stdout=test2_out_real, stderr=test2_err_real, ignoreNonZeroExitCode=true);

        // Check output.
        bool exit_code2_ok = (exit_code2 == 0);
        bool file2_written = isfile(file2_out_real) and filesize(file2_out_real) > 0;
        bool check_ok = exit_code2_ok and file2_written;

        // Cleanup.
        if check_ok:
            rmfile(test2_out_real);
            rmfile(test2_err_real);
            if exists(file2_out_real):: rmfile(file2_out_real);
        end

        // Update counts.
        check_count = check_count + 1;
        if     check_ok:: check_successes = check_successes + 1;
        if not check_ok:: check_failures  = check_failures  + 1;
    end

    // ========
    // Cleanup.
    // ========

    if not file1_diff:: if exists(file1_out_real):: rmfile(file1_out_real);
end

// Get result messages.
string trans_rslt;
string check_rslt;
if trans_failures == 0: trans_rslt = "SUCCESS"; else trans_rslt = "FAILURE"; end
if check_failures == 0: check_rslt = "SUCCESS"; else check_rslt = "FAILURE"; end

string trans_msg = fmt("Test %s (%s, transform): %d tests, %d successes, %d failures, %d skipped.",
                       trans_rslt, test_path, trans_count, trans_successes, trans_failures, trans_skipped);
string check_msg = fmt("Test %s (%s, check):     %d tests, %d successes, %d failures.",
                       check_rslt, test_path, check_count, check_successes, check_failures);

// Output result messages.
if trans_failures == 0:
    outln(trans_msg);
else
    errln(trans_msg);
end

if check_failures == 0:
    outln(check_msg);
else
    errln(check_msg);
end

// Return number of failures as exit code. No failures means zero exit code,
// any failures means non-zero exit code.
exit trans_failures + check_failures;
